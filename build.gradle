buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

repositories {
    google()
    jcenter()
    mavenCentral()
}

configurations {
    deployAars
}


group = "io.outbound.sdk"
version = "0.4.0"
def siteUrl = 'https://outboundio.github.io/android-sdk'
def gitUrl = 'https://github.com/outboundio/android-sdk.git'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-annotations:27.0.2'
    compile 'com.android.support:support-compat:27.0.2'

    // This is needed to stop conflicts - otherwise the firebase-messaging:11.8.0 dependency will
    // pull in v25 of the support library which will cause issues.   This line pins it at v27
    // which matches the support annotations and compat which we use above.
    compile 'com.android.support:support-v4:27.0.2'

    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    compile 'com.google.firebase:firebase-messaging:11.8.0'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                name name
                url siteUrl
                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id 'tang8330'
                        name 'Robin Tang'
                        email 'robin@outbound.io'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "outbound-android-sdk"
        userOrg = "outbound"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = false
    }
}
