//buildscript {
//    repositories {
//        google()
//        jcenter()
//        mavenCentral()
//    }
//    dependencies {
//        classpath 'com.android.tools.build:gradle:3.0.1'
//        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
//        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
//    }
//}
//
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

repositories {
    google()
    jcenter()
    mavenCentral()
}

configurations {
    deployAars
}


group = "io.outbound.sdk"
version = "0.3.0"
def siteUrl = 'https://outboundio.github.io/android-sdk'
def gitUrl = 'https://github.com/outboundio/android-sdk.git'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:${rootProject.ext.androidSupportLibraryVersion}"
    implementation "com.android.support:support-annotations:${rootProject.ext.androidSupportLibraryVersion}"
    implementation "com.google.code.gson:gson:2.8.0"
    implementation "com.squareup.okhttp3:okhttp:3.9.0"
    implementation "com.google.firebase:firebase-messaging:${rootProject.ext.firebaseVersion}"
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                name name
                url siteUrl
                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id 'tang8330'
                        name 'Robin Tang'
                        email 'robin@outbound.io'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "outbound-android-sdk"
        userOrg = "outbound"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = false
    }
}
