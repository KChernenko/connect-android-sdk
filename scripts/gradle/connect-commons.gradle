ext {
    /**
     * Looks for the specified configuration key in this order:
     *
     * <ol>
     *     <li>system environment</li>
     *     <li>project properties</li>
     * </ol>
     *
     * If it isn't found, then the empty string is returned. This stops the gradlew from failing until
     * it actually needs those values.
     *
     * @param keyName The key to look for
     * @return The value or the empty string if not found
     */
    getConfigurationParameter = { keyName ->
        return System.getenv(keyName) ?: rootProject.hasProperty(keyName) ? rootProject.properties[keyName] : ''
    }

    /**
     * Checks if we should use the local modules  as dependency
     * <p>
     *     Test mode is based on the existence of an environmental variable called LOCAL_BUILD
     * <p>
     *
     * @return if local dependencies should be used true, otherwise false
     */
    useLocalDependency = {
        if("$System.env.LOCAL_BUILD" != "null") {
            return System.env.LOCAL_BUILD.toBoolean()
        }

        return rootProject.ext.buildSettings.localBuild
    }

    /**
     * Checks if we should use release dependencies. This normally means using JARs instead
     * of using local modules while building modules for release.
     *
     * @return true if release dependencies should be used
     */
    useReleaseDependency = {
        return rootProject.ext.buildSettings.releaseBuild
    }
}